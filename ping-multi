#!/usr/bin/perl
use strict;
use warnings;

use Net::Ping;

use threads;
use threads::shared;

use Getopt::Long qw(GetOptions);
Getopt::Long::Configure qw(gnu_getopt);

use Curses;
use JSON::PP;
use Time::HiRes qw(sleep usleep time);
use POSIX;
use Statistics::Descriptive;
use File::Basename;

my $timeout = 1;
my $ping_interval = 1;
my %ping_data : shared = ();
my %resolved_hosts : shared = ();
my $sync_ping_cnt : shared = 0;
my @pressed_keys = ();
my @thr_handles = (); # used in the SIG handlers
my $scr_max = {
	'x' => undef,
	'y' => undef,
};
my $hdr_len = {
	'host' => 20,
	'sep' => 2,
	'rtt' => '6',
	'history' => undef,
};
my @stats_selector = qw/ Last Loss% Avg Min Max StDev RX_cnt TX_cnt XX_cnt /;
my @sort_selector = ('None', 'Asc', 'Desc');
my $host_meta = {};
my %config : shared = (
	'print_resolved_ip' => 0,
	'show_scale' => 0,
	'scale_ms' => undef,
	'pause' => 0,
	'sync_ping' => 0,
);
my $hosts_print_line_offset = 3; # const
my $logfile = "";

sub ping_host {
	my $host = shift;
	my $pinger = Net::Ping->new('icmp', $timeout);
	my ($success, $rtt, $ip);
	my $tdata;
	my ($pinger_target, $needs_resolve);

	$pinger->hires();

	while (1) {
		my $ping_begin = time;
		if (!$config{'pause'}) {
			{
				lock(%resolved_hosts);
				if (!exists($resolved_hosts{$host}) || !defined($resolved_hosts{$host})) {
					$pinger_target = $host;
					$needs_resolve = 1;
				} else {
					$pinger_target = $resolved_hosts{$host};
					$needs_resolve = 0;
				}
			}
			($success, $rtt, $ip) = $pinger->ping($pinger_target);

			# discard if pause was hit, and we were in progress ping'ing
			if (!$config{'pause'}) {
				if (defined($rtt)) {
					$rtt *= 1000; # convert to "ms"
				}
				
				{
					lock(%ping_data);
					$tdata = decode_json($ping_data{$host});
					push(@{$tdata}, {
						'success' => $success,
						'rtt' => $rtt,
						'ip' => $ip,
						'stats_new' => 1,
					});
					$ping_data{$host} = encode_json($tdata);
				}
				if ($needs_resolve) {
					lock(%resolved_hosts);

					if ($success) {
						$resolved_hosts{$host} = $ip;
					} else {
						$resolved_hosts{$host} = undef;
					}
				}
			}
		}

		if ($config{'sync_ping'}) {
			lock($sync_ping_cnt);
			$sync_ping_cnt++;
			cond_wait($sync_ping_cnt) until $sync_ping_cnt == 0;
		}

		# sleep till one second from ping beginning elapsed.
		my $sleep_time = $ping_interval - (time - $ping_begin);
		sleep($sleep_time) if ($sleep_time > 0);
	}

	#$pinger->close(); // we are exiting anyway
}

sub getch_blocking { # thanks to halfdelay() we block with a timeout
	my ($ch, $key);
	while (1) {
		($ch, $key) = getch();
		if (defined($key)) { # special key
			next;
		}
		if (!defined($ch)) {
			last; # getch() error
		}
		if ($ch eq '-1') {
			last; # no key pressed
		}

		# defined($ch) && $ch != -1

		push(@pressed_keys, $ch);
	}
}

sub init_curses() {
	initscr();
	noecho();
	start_color();
	curs_set(0);
	halfdelay(1); # makes getch() to block with a timeout

	$scr_max->{'y'} =  getmaxy(stdscr);
	$scr_max->{'x'} =  getmaxx(stdscr);
}

sub make_last_element_first {
	my $arr = shift @_;
	my $reverse = shift @_;

	if (!$reverse) {
		push(@{$arr}, shift @{$arr});
	} else {
		unshift(@{$arr}, pop @{$arr});
	}
}

sub handle_pressed_keys() {
	my $key;
	while (scalar @pressed_keys) {
		$key = shift @pressed_keys;

		if (lc($key) eq 'd') {
			$config{'print_resolved_ip'} = !$config{'print_resolved_ip'};
		}
		if ($key eq 's') {
			make_last_element_first(\@stats_selector, 0);
		}
		if ($key eq 'S') {
			make_last_element_first(\@stats_selector, 1);
		}
		if ($key eq 'o') {
			make_last_element_first(\@sort_selector, 0);
		}
		if ($key eq 'O') {
			make_last_element_first(\@sort_selector, 1);
		}
		if (lc($key) eq 't') {
			$config{'show_scale'} = !$config{'show_scale'};
		}
		if (lc($key) eq 'p') {
			$config{'pause'} = !$config{'pause'};
		}
		if (lc($key) eq 'q') {
			exit_curses_and_join_threads();
			exit(0);
		}
	}
}

sub init_structs_for_host {
	my $line = shift @_;
	my $pos = (scalar keys %{$host_meta}) + $hosts_print_line_offset;

	if (exists($host_meta->{$line})) {
		return;
	}

	$host_meta->{$line} = {
		'line_pos' => $pos,
		'stats' => Statistics::Descriptive::Full->new(),
		'pkt_sent' => 0,
		'pkt_recv' => 0,
		'init_line_pos' => $pos,
		'previous' => -1,
	};

	$ping_data{$line} = encode_json([]);
}

sub read_list_file_and_init_structs {
	my $list_file = shift @_;
	my $line;

	open(HOSTLIST, '<', $list_file) or die("open($list_file): $!\n");
	while ($line = <HOSTLIST>) {
		if ($line =~ /^\s*$/ || $line =~ /^\s*#/) {
			next; # skip empty and commented out lines
		}
		# trim() white-spaces
		$line =~ s/^\s+//;
		$line =~ s/\s+$//;

		init_structs_for_host($line);
	}
	close(HOSTLIST) or die("close($list_file): $!\n");
}

sub start_ping_threads {
	my $host;
	my $thr;

	foreach $host (keys %{$host_meta}) {
		$thr = threads->create(\&ping_host, $host);
		push(@thr_handles, $thr);
	}
}

sub print_host_headers {
	my $host;
	my $pos;
	my $s;

	foreach $host (keys %{$host_meta}) {
		if (!$config{'print_resolved_ip'}) {
			$s = $host;
		} else {
			lock(%resolved_hosts);
			if (!exists($resolved_hosts{$host})) {
				$s = $host;
			} else {
				if (defined($resolved_hosts{$host})) {
					$s = $resolved_hosts{$host};
				} else {
					$s = "DNS resolve failed";
				}
			}
		}

		$s = substr($s, 0, $hdr_len->{'host'}); # cut, if needed
		addstr(
			$host_meta->{$host}->{'line_pos'}, 0,
			$s
		);

		# clear up stale text
		$pos = length($s);
		if ($pos < $hdr_len->{'host'}) {
			addstr(
				$host_meta->{$host}->{'line_pos'}, $pos,
				' 'x($hdr_len->{'host'} - $pos)
			);
		}
	}
}

sub calc_hdr_lengths {
	$hdr_len->{'history'} = $scr_max->{'x'} -
		$hdr_len->{'host'} - $hdr_len->{'sep'} - $hdr_len->{'rtt'} -
		$hdr_len->{'sep'};
	
	if ($hdr_len->{'history'} < 10) {
		die("Your screen is too small (columns=".$scr_max->{'x'}.").\n");
	}
}

sub _print_ping_sample_scale {
	my $sample = shift @_;
	my $num;

	if (!$config{'show_scale'}) {
		if ($sample->{'success'}) {
			return '.';
		} else {
			if (!$sample->{'ip'}) {
				return 'E'; # General error
			} else {
				return 'X';
			}
		}
	}

	# show scale

	if (!$sample->{'success'}) {
		if (!$sample->{'ip'}) {
			return 'E'; # General error
		} else {
			return '-';
		}
	}

	$num = floor($sample->{'rtt'} / $config{'scale_ms'});
	if ($num > 9) { # this should never happen
		$num = '>';
	} elsif ($num < 0) { # this should never happen
		$num = '<';
	}

	return $num;
}

sub print_logfile {
	my $now = shift @_;
	my $host = shift @_;
	my $status = shift @_;

	print LOGFILE "$now $host $status\n" or die("print($logfile): $!");
}

sub _print_ping_samples {
	my $host = shift @_;
	my @all_samples = @{shift @_};
	my $pos;
	my $i;
	my $ch;
	my $sample;
	my $now;

	if ($logfile ne "") {
		my ($second, $minute, $hour, $day, $month, $year) = (localtime(time))[0..5];
		$now = sprintf ("%d-%02d-%02d %02d:%02d:%02d", $year + 1900, $month + 1, $day, $hour, $minute, $second);
	}

	$pos = $scr_max->{'x'} - $hdr_len->{'history'};
	for ($i = 0; $i < $hdr_len->{'history'}; ++$i, ++$pos) {
		if ($i >= scalar @all_samples) { # no more data
			$ch = ' ';
		} else {
			$sample = $all_samples[$i];

			$ch = _print_ping_sample_scale($sample);

			if ($sample->{'stats_new'}) {
				$sample->{'stats_new'} = 0;
				$host_meta->{$host}->{'pkt_sent'} += 1;
				if ($host_meta->{$host}->{'previous'} < 0) {
					$host_meta->{$host}->{'previous'} = $sample->{'success'};
					if ($logfile ne "") {
						if ($sample->{'success'}) {
							print_logfile($now, $host, "STARTSUP");
						} else {
							print_logfile($now, $host, "STARTSDOWN");
						}
					}
				}
				if ($sample->{'success'}) {
					$host_meta->{$host}->{'stats'}->add_data($sample->{'rtt'});
					$host_meta->{$host}->{'pkt_recv'} += 1;
				}
				if ($host_meta->{$host}->{'previous'} ne $sample->{'success'}) {
					$host_meta->{$host}->{'previous'} = $sample->{'success'};
					if ($logfile ne "") {
						if ($sample->{'success'}) {
							print_logfile($now, $host, "UP");
						} else {
							print_logfile($now, $host, "DOWN");
						}
					}
				}
			}

			# update the global data
			$all_samples[$i] = $sample;
		}
		addstr(
			$host_meta->{$host}->{'line_pos'},
			$pos,
			$ch
		);
	}
}

sub _get_rtt_sample_stats {
	my $sample = shift @_;
	my $host = shift @_;
	my ($val, $raw_val);
	my $st = $stats_selector[0];
	my $decpt = 1;
	my $suffix = '';

	if ($st eq "Last") {
		$val = $sample->{'success'} ? $sample->{'rtt'} : undef;
	}
	elsif ($st eq "Loss%") {
		if ($host_meta->{$host}->{'pkt_sent'} == 0) {
			$val = undef;
		} else {
			$val = (
				$host_meta->{$host}->{'pkt_recv'}
				/
				$host_meta->{$host}->{'pkt_sent'}
				*
				100
			);
			$val = 100 - $val;
			$decpt = 0; # fmt
			$suffix = '%';
		}
	}
	elsif ($st eq "Avg") {
		$val = $host_meta->{$host}->{'stats'}->mean();
	}
	elsif ($st eq "Min") {
		$val = $host_meta->{$host}->{'stats'}->min();
	}
	elsif ($st eq "Max") {
		$val = $host_meta->{$host}->{'stats'}->max();
	}
	elsif ($st eq "StDev") {
		$val = $host_meta->{$host}->{'stats'}->standard_deviation();
	}
	elsif ($st eq "RX_cnt") {
		$val = $host_meta->{$host}->{'stats'}->count();
		if ($val != $host_meta->{$host}->{'pkt_recv'}) {
			die("Sanity check failed");
		}
		$decpt = 0; # fmt
	}
	elsif ($st eq "TX_cnt") {
		$val = $host_meta->{$host}->{'pkt_sent'};
		$decpt = 0; # fmt
	}
	elsif ($st eq "XX_cnt") { # Lost packets count
		$val = $host_meta->{$host}->{'pkt_sent'} - $host_meta->{$host}->{'pkt_recv'};
		$decpt = 0; # fmt
	}
	else {
		die("Bad stats_selector value: $st");
	}

	$raw_val = $val;
	if (!defined($val)) {
		$val = '-';
	} else {
		$val = sprintf('%.'.$decpt.'f', $val);
	}
	return (sprintf('%'.$hdr_len->{'rtt'}.'s', $val . $suffix), $raw_val, $suffix);
}

sub get_ping_rtt_info {
	my $host = shift @_;
	my @all_samples = @{shift @_};
	my $sample;
	my $ch;
	my $val = undef;
	my $suffix = undef;

	$ch = sprintf(
		'%'.$hdr_len->{'rtt'}.'s',
		''
	);
	if (scalar @all_samples > 0) {
		$sample = $all_samples[scalar @all_samples - 1]; # last sample
		($ch, $val, $suffix) = _get_rtt_sample_stats($sample, $host);
	}

	return ($ch, $val, $suffix);
}

sub _print_ping_rtt_info {
	my $host = shift @_;
	my $all_samples_ref = shift @_;
	my $ch;

	($ch, undef, undef) = get_ping_rtt_info($host, $all_samples_ref);

	addstr(
		$host_meta->{$host}->{'line_pos'},
		$hdr_len->{'host'} + $hdr_len->{'sep'},
		$ch
	);
}

sub print_ping_data {
	my $host;
	my @all_samples;

	foreach $host (keys %ping_data) {
		@all_samples = @{decode_json($ping_data{$host})};

		if (scalar @all_samples > $hdr_len->{'history'}) {
			# cut into half; save only the more recent entries
			@all_samples = splice @all_samples, scalar(@all_samples)/2;
		}

		_print_ping_samples($host, \@all_samples);
		_print_ping_rtt_info($host, \@all_samples);

		# update the global data
		$ping_data{$host} = encode_json(\@all_samples);
	}
}

sub trigger_sync_ping {
    if (!$config{'sync_ping'}) {
        return;
    }

    lock($sync_ping_cnt);
    lock(%ping_data);

    if ($sync_ping_cnt < keys %ping_data) {
        return ;
    }

    $sync_ping_cnt = 0;
    cond_broadcast($sync_ping_cnt);
}

sub print_main_headers() {
	my $line;
	my $fmt;
	my $host_title;
	my $s;
	my $item;
	my $pos;
	my $arrow;
	my $stats_title;
	my $arrow_placeholder = "\1";

	$fmt = '%-'.$hdr_len->{'host'}.'s%'.$hdr_len->{'sep'}.'s%'.
		$hdr_len->{'rtt'}.'s%'.$hdr_len->{'sep'}.'s%-'.
		$hdr_len->{'history'}.'s';

	if ($config{'print_resolved_ip'}) {
		$host_title = 'IP address';
	} else {
		$host_title = 'Hostname';
	}

	$stats_title = $stats_selector[0];
	if ($sort_selector[0] ne 'None') {
		$stats_title .= $arrow_placeholder; # leave space for the arrow
	}

	$line = sprintf($fmt,
		$host_title, '',
		$stats_title, '',
		'Ping history'.($config{'show_scale'} ? ' with scale per '.$config{'scale_ms'}.' ms' : '')
	);

	if ($sort_selector[0] ne 'None') {
		# https://ux.stackexchange.com/questions/37564/use-up-or-down-arrow-to-represent-sort-ascending-at-table-header
		if ($sort_selector[0] eq 'Asc') {
			$arrow = '▲';
		} else {
			$arrow = '▼';
		}
		$line =~ s/$arrow_placeholder/$arrow/;
	}

	attron(A_BOLD);
	addstr(2, 0, $line);
	attroff(A_BOLD);
	
	$s = "Ping-Multi keys: ".
		"DNS resolve | Stats | Order by | ".
		"Time scale | Pause ".
		($config{'pause'} ? 'on ' : 'off')." | Quit";
	addstr(0, 0, $s);
	attron(A_BOLD);
	for $item (qw/ DNS Stats Time Pause Order Quit /) {
		$pos = index($s, $item);
		if ($pos == -1) {
			die("Unable to find token to make bold: $item");
		}
		addstr(0, $pos, substr($item, 0, 1));
	}
	attroff(A_BOLD);
}

sub sort_hosts() {
	my $host;
	my @sorted_hosts;
	my $all_samples_ref;
	my $host_rtt = {};
	my $pos;
	my $val;
	my $dm = 1;

	foreach $host (keys %ping_data) {
		$all_samples_ref = decode_json($ping_data{$host});
		(undef, $val, undef) = get_ping_rtt_info($host, $all_samples_ref);
		$host_rtt->{$host} = $val;
	}

	@sorted_hosts = sort {
		# use default init order if no "Order by" is selected
		if ($sort_selector[0] eq 'None') {
			return
				$host_meta->{$a}->{'init_line_pos'}
				<=>
				$host_meta->{$b}->{'init_line_pos'}
			;
		}

		# undefined hosts are always at the bottom
		if (!defined($host_rtt->{$a}) && !defined($host_rtt->{$b})) {
			return
				$host_meta->{$a}->{'init_line_pos'}
				<=>
				$host_meta->{$b}->{'init_line_pos'}
			;
		}
		if (!defined($host_rtt->{$a})) {
			return +1;
		}
		if (!defined($host_rtt->{$b})) {
			return -1;
		}

		sub sub_sort {
			my $a = shift @_;
			my $b = shift @_;
			my $host_rtt = shift @_;
			my $host_meta = shift @_;

			if ($host_rtt->{$a} != $host_rtt->{$b}) {
				return $host_rtt->{$a} <=> $host_rtt->{$b};
			} else {
				# on equal RTT values, fall back to init line position
				return
					$host_meta->{$a}->{'init_line_pos'}
					<=>
					$host_meta->{$b}->{'init_line_pos'}
				;
			}
		};

		my $res = sub_sort($a, $b, $host_rtt, $host_meta);

		if ($sort_selector[0] eq 'Desc') {
			return -$res;
		} else {
			return $res;
		}
	} keys %{$host_meta};

	$pos = $hosts_print_line_offset;
	foreach $host (@sorted_hosts) {
		$host_meta->{$host}->{'line_pos'} = $pos;
		++$pos;
	}
}

sub usage() {
	die(
		"Usage: ".basename($0)." [-S] [-f FILE] [-W timeout] [HOST]...\n".
		"Ping all hosts from FILE and HOSTs.\n\n".
		"  -f, --file=FILE     read list of hosts from file\n".
		"  -h, --help          display this help and exit\n".
		"  -S, --sync-ping     send the same amount of ping requests for all hosts\n".
		"  -W, --wait=SECS     timeout in seconds to wait for a ping reply\n".
		"  -i, --interval=SECS time in seconds between sending each request\n".
		"  -l, --logfile=FILE  log events to file\n"
	);
}

sub main() {
	my $file = '';
	my $help;

	GetOptions(
	    'file|f=s' => \$file,
	    'help|h' => \$help,
	    'sync-ping|S' => \$config{'sync_ping'},
	    'wait|W=f' => \$timeout,
	    'interval|i=f' => \$ping_interval,
	    'logfile|l=s' => \$logfile
	);

	if ($help) {
		usage();
	}

	if (scalar @ARGV == 0 && $file eq '') {
		usage();
	}

	if ($logfile ne "") {
		open(LOGFILE, ">>", $logfile) || die("Can't open logfile \"$logfile\": $!\n");
		# print to file immediately (no buffering)
		#   https://perl.plover.com/FAQs/Buffering.html
		my $ofh = select LOGFILE;
		$| = 1;
		select $ofh;
	}

	$config{'scale_ms'} = floor($timeout * 1000 / 10); # 0..9

	if ($file ne '') {
		read_list_file_and_init_structs($file);
	}

	foreach my $ip (@ARGV) {
		init_structs_for_host($ip);
	}
	start_ping_threads();

	init_curses();
	calc_hdr_lengths();

	while(1) {
		getch_blocking(); # with a very short timeout
		handle_pressed_keys();

		{
			lock(%ping_data);

			sort_hosts();

			print_main_headers();
			print_host_headers();
			print_ping_data();
		}

		trigger_sync_ping();

		if (scalar threads->list(threads::running) != scalar keys %{$host_meta}) {
			die("A worker thread exited unexpectedly.");
		}

		refresh();

		sleep(0.3);
	}
}

sub exit_curses_and_join_threads {
	endwin();

	for (@thr_handles) {
		$_->detach(); # discard exit status
	}
}

$SIG{__WARN__} = sub { # make warn() fatal
	die($_[0]);
};

$SIG{__DIE__} = sub {
	exit_curses_and_join_threads();
};

$SIG{INT} = sub {
	exit_curses_and_join_threads();
	exit(0);
};

main();
